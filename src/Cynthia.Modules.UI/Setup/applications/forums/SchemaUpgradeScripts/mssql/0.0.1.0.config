CREATE PROCEDURE [dbo].cy_ForumSubscriptions_GetCount

/*
Author:   			Joe Audette
Created: 			2009-12-12
Last Modified: 		2009-12-12
*/

@ForumID int

AS

SELECT COUNT(*) 
FROM [dbo].[cy_ForumSubscriptions]
WHERE ForumID = @ForumID
AND UnSubscribeDate IS NULL

GO

CREATE PROCEDURE [dbo].cy_ForumSubscriptions_SelectPage

-- Author:   			Joe Audette
-- Created: 			2009-12-12
-- Last Modified: 		2009-12-12

@ForumID int,
@PageNumber 			int,
@PageSize 			int

AS

DECLARE @PageLowerBound int
DECLARE @PageUpperBound int


SET @PageLowerBound = (@PageSize * @PageNumber) - @PageSize
SET @PageUpperBound = @PageLowerBound + @PageSize + 1


CREATE TABLE #PageIndex 
(
	IndexID int IDENTITY (1, 1) NOT NULL,
SubscriptionID Int
)

BEGIN

INSERT INTO #PageIndex ( 
SubscriptionID
)

SELECT
		fs.[SubscriptionID]
		
FROM
		[dbo].[cy_ForumSubscriptions] fs
		
LEFT OUTER JOIN
		[dbo].cy_Users u
ON
		u.UserID = fs.UserID
		
WHERE
	fs.ForumID = @ForumID
	AND fs.UnSubscribeDate IS NULL

ORDER BY
	u.[Name]

END


SELECT
		t1.*,
		u.[Name],
		u.LoginName,
		u.Email
		
FROM
		[dbo].[cy_ForumSubscriptions] t1

JOIN			#PageIndex t2
ON			
		t1.[SubscriptionID] = t2.[SubscriptionID]
		
LEFT OUTER JOIN
		[dbo].cy_Users u
ON
		u.UserID = t1.UserID
		
WHERE
		t2.IndexID > @PageLowerBound 
		AND t2.IndexID < @PageUpperBound
		
ORDER BY t2.IndexID

DROP TABLE #PageIndex

GO
